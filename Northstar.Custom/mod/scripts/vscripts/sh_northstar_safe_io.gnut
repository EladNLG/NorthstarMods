globalize_all_functions

table< int, void functionref( string ) > pendingCallbacks
table< int, void functionref( table ) > pendingJSONCallbacks
table< int, void functionref() > failedCallbacks


void function NSLoadFile( string file, void functionref( string ) onSuccess, void functionref() onFailure = null )
{
    int handle = NS_InternalLoadFile( file )

    pendingCallbacks[handle] <- onSuccess
    if (onFailure != null)
        failedCallbacks[handle] <- onFailure
}

void function NSLoadJSONFile( string file, void functionref( table ) onSuccess, void functionref() onFailure = null )
{
    int handle = NS_InternalLoadFile( file )

    pendingJSONCallbacks[handle] <- onSuccess
    if (onFailure != null)
        failedCallbacks[handle] <- onFailure
}

void function NSHandleLoadResult( int handle, bool success, string result )
{
    if (!success)
    {
        if (handle in failedCallbacks)
        {
            failedCallbacks[handle]()
            delete failedCallbacks[handle]
            return
        }
    }
    if (handle in pendingCallbacks)
    {
        pendingCallbacks[handle](result)
        delete pendingCallbacks[handle]
        return
    }
    if (handle in pendingJSONCallbacks)
    {
        // result may be an empty string if the load fails, so we return an empty table.
        if (result == "")
            pendingJSONCallbacks[handle]({})
        else pendingJSONCallbacks[handle](DecodeJSON(result))
        delete pendingJSONCallbacks[handle]
        return
    }
    throw "Invalid IO callback handle"
    unreachable
}

array<string> function NSGetAllFiles( string path = "" )
{
    return NS_InternalGetAllFiles(path)
}